buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta6'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

ext {
    nexusCredentialsAvailable = project.hasProperty('nexusUsername') && project.hasProperty('nexusPassword')
}

dependencies {
    repositories {
        mavenCentral()
    }
    compile 'com.android.support:support-v4:23.2.0'
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}


signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

group = 'com.github.unisontech.afilechooser'
version = '1.14'

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        repository(url: "http://repo.spb.unison.com/content/repositories/afilechooser/") {
            if (nexusCredentialsAvailable) {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
        }
        snapshotRepository(url: "http://repo.spb.unison.com/content/repositories/snapshots/") {
            if (nexusCredentialsAvailable) {
                authentication(userName: nexusUsername, password: nexusPassword)
            }
        }




        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        pom.project {
            name 'aFileChooser Library'
            packaging 'aar'
            description 'aFileChooser library for Android applications'
            url 'https://github.com/unisontech/aFileChooser'

            scm {
                url 'scm:git@github.com:unisontech/aFileChooser.git'
                connection 'scm:git@github.com:unisontech/aFileChooser.git'
                developerConnection 'scm:git@github.com:unisontech/aFileChooser.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'ipaulpro'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    repositories.mavenInstaller {
        configuration = configurations.default
        pom.artifactId = 'aFileChooser'
    }
}
